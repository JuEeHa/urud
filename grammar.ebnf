@@grammar :: Urud

@@eol_comments :: /#.*?$/

@@keyword :: let fun

start = {definition}* $;

@name
identifier = /(?!\d)\w+/;

type = identifier '<' type {',' type}* '>' | type '[' identifier {',' identifier} ']' | '*' identifier | identifier;

literal = integer_literal | string_literal;
integer_literal = /[-+]?[\d]+/;
string_literal = "'" /([^'\\]|\\.)*/ "'";

function_call = atom '(' [expression {',' expression}*] ')';

addsub_operator = muldiv_operator {('+' | '-') muldiv_operator};
muldiv_operator = atom {('*' | '/') atom};

atom = function_call | literal | identifier | '(' expression ')';

expression = addsub_operator | atom;

variable_definition = 'let' identifier '=' expression;

# Use "(identifier | type identifier)" instead of "[type] identifier" as latter confuses parser.
# TODO: Investigate why
function_definition = 'fun' (identifier | type identifier) '(' [type identifier {',' type identifier}*] ')' {expression | definition}+ ';';

interface_definition = 'interface' type identifier '(' [type {',' type}*] ')';

definition = variable_definition | function_definition | interface_definition;
